buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'io.fabric'

ext {
    archRoomVersion = '1.0.0'
    archLifecycleVersion = '1.1.1'
    firebaseVersion = '11.8.0'
    supportLibraryVersion = '27.1.0'
    stethoVersion = '1.5.0'
    uniqueVersionCode = { version, suffix -> Integer.parseInt(String.format("%s%02d", version.replaceAll("[^0-9]", ""), suffix)) }
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "tk.andrielson.carrinhos.androidapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 4
        versionName "0.0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    flavorDimensions "CLIENTE", "VERSAO"
    productFlavors {
        casadosorvetesnp {
            dimension "CLIENTE"
            applicationIdSuffix = "casadosorvetesnp"
        }
        TESTE {
            dimension "VERSAO"
            applicationIdSuffix = "teste"
            buildConfigField("boolean", "TESTE", 'true')
        }
        PRODUCAO {
            dimension "VERSAO"
            buildConfigField("boolean", "TESTE", 'false')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "DESENVOLVIMENTO", 'false')
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "DESENVOLVIMENTO", 'true')
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def cliente = variant.productFlavors[0].name
            def modo = variant.productFlavors[1].name
            def version = variant.versionName
            output.outputFileName = "Carrinhos" + cliente + "_" + modo + "_" + version + ".apk"
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Android Architeture Lifecycle
    implementation "android.arch.lifecycle:extensions:$archLifecycleVersion"
    implementation "android.arch.lifecycle:common-java8:$archLifecycleVersion"

    // Android Architeture Room Persistence Library
    implementation "android.arch.persistence.room:runtime:$archRoomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archRoomVersion"

    //Firebase (versão 12.0.0 está com bug na @PropertyName)
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.firebase:firebase-firestore:$firebaseVersion"
    implementation 'com.firebaseui:firebase-ui-firestore:3.2.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    // Facebook Stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"

    // Fabric Answers
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }

    // Ferramentas de teste
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'org.jetbrains:annotations-java5:15.0'
}
apply plugin: 'com.google.gms.google-services'
